plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
    id 'org.cyclonedx.bom' version "1.4.1"
}

group = 'com.github.pjfanning'
version = '2.3.3-SNAPSHOT'

description = 'Memory efficient Shared String Table and Comments Table for POI'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'com.h2database:h2:2.0.204'
    implementation 'org.apache.poi:poi-ooxml:5.1.0'
    implementation 'org.apache.poi:poi:5.1.0'
    implementation 'org.apache.xmlbeans:xmlbeans:5.0.2'
    implementation 'org.apache.commons:commons-text:1.9'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.nanohttpd:nanohttpd:2.3.1'
    testImplementation 'commons-io:commons-io:2.11.0'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.32'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-to-slf4j:2.17.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'POI Shared Strings'
                description = 'Memory efficient Shared Strings Table implementation for POI streaming'
                url = 'https://github.com/pjfanning/poi-shared-strings'
                inceptionYear = '2018'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'pjfanning'
                        name = 'PJ Fanning'
                    }
                }
                scm {
                    url = 'https://github.com/pjfanning/poi-shared-strings'
                    connection = 'scm:git://github.com/pjfanning/poi-shared-strings.git'
                    developerConnection = 'scm:git://github.com/pjfanning/poi-shared-strings.git'
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
