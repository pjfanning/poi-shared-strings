plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group = 'com.github.pjfanning'
version = '2.3.0-SNAPSHOT'

description = 'Memory efficient Shared String Table and Comments Table for POI'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'com.h2database:h2:1.4.200'
    implementation 'org.apache.poi:poi-ooxml-lite:5.1.0'
    implementation 'org.apache.poi:poi:5.1.0'
    implementation 'org.apache.xmlbeans:xmlbeans:5.0.2'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation files('libs/poi-ooxml-5.2.0-SNAPSHOT.jar')
    implementation 'com.github.virtuald:curvesapi:1.06'
    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.santuario:xmlsec:2.3.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.nanohttpd:nanohttpd:2.3.1'
    testImplementation 'commons-io:commons-io:2.11.0'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.32'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-to-slf4j:2.14.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'POI Shared Strings'
                description = 'Memory efficient Shared Strings Table implementation for POI streaming'
                url = 'https://github.com/pjfanning/poi-shared-strings'
                inceptionYear = '2018'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'pjfanning'
                        name = 'PJ Fanning'
                    }
                }
                scm {
                    url = 'https://github.com/pjfanning/poi-shared-strings'
                    connection = 'scm:git://github.com/pjfanning/poi-shared-strings.git'
                    developerConnection = 'scm:git://github.com/pjfanning/poi-shared-strings.git'
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
